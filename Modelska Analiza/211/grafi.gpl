set terminal epslatex color solid

set output "g_energija_zacetek.tex"
set xlabel "\"Stevilo korakov"
set ylabel "Kineti\"cna energija"

set logscale y

plot for [i in "10 30 100 300 1000"] sprintf("g_energija_zacetek_%s.dat", i) u 1:($3+$4) w lines t sprintf("$J = %s$", i)

set output "g_energija.tex"

set xlabel "\"Stevilo korakov ($\\times 1000$)"
set ylabel "Odstopanje energije $E-E_0$"
set key at 750, 0.03
unset logscale

plot for [i in "10 30 100 300 1000"] sprintf("g_energija_%s.dat", i) u ($1/1000):($2+$3+$4-$5) w lines t sprintf("$J = %s$", i)

set output "g_energija_log.tex"
set ylabel "Odstopanje energije $|E-E_0|$"
set logscale y
set key default
set key bottom right

plot for [i in "10 30 100 300 1000"] sprintf("g_energija_%s.dat", i) u ($1/1000):(abs($2+$3+$4-$5)) w lines t sprintf("$J = %s$", i)

unset logscale
set output "g_energija_korak.tex"

set xlabel "\"Cas $t$"
set ylabel "Odstopanje energije $E-E_0$"
unset key
set key at 8.4,0.0025

plot for [i in "1 4 10 40 100"] sprintf("g_energija_korak_%s.dat", i) u 1:($2+$3+$4-$5) w lines t sprintf("$k = %s \\cdot 10^{-5}$", i)

