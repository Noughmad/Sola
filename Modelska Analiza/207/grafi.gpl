set terminal epslatex color solid
set fit errorvariables


set xlabel "\"Stevilo to\"ck $n$"
set ylabel "Pretok $\\Phi$"

set key bottom right

b = -1
b2 = -1
phi(x) = Phi - a*x**b

set output "g_pretok_polkrog.tex"
fit phi(x) "g_konv_srediscna.dat" u 2:3 via Phi,a,b

plot "g_konv_srediscna.dat" u 2:3 t "Rezultat", \
phi(x) t sprintf("$\\Phi_0 = %.7f \\pm %.7f$", Phi, Phi_err)

set output "g_pretok_kvadrat.tex"
fit phi(x) "g_konv_batman.dat" u 2:3 via Phi,a,b
plot "g_konv_batman.dat" u 2:3 t "Rezultat", \
phi(x) t sprintf("$\\Phi_0 = %.7f \\pm %.7f$", Phi, Phi_err)

set output "g_hitrost.tex"

set logscale xy
set ylabel "\"Cas ra\"cunanja [s]"

C = 0.001
f(x) = C*x**p
set key top left

fit[100:] f(x) "< cat g_konv_srediscna.dat g_konv_batman.dat" u 2:4 via C,p

plot[100:200000] "g_konv_srediscna.dat" u 2:4 t "Polkrog", \
"g_konv_batman.dat" u 2:4 t "Izrezan kvadrat", \
f(x) t sprintf("$p = %g$", p)