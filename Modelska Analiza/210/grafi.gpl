set terminal epslatex color solid

set output "g_hitrost.tex"
set logscale xy
set xlabel "$n$"
set ylabel "Cas [s]"
set key bottom right

dva(x) = B*x**2
ena(x) = D*x**3
tri(x) = F*x**4

fit ena(x) "g_cas.dat" u 1:3:(sqrt($3)) via D
fit dva(x) "g_cas.dat" u 1:5:(sqrt($5)) via B
fit[1:1000] tri(x) "g_cas.dat" u 1:7:(sqrt($7)) via F

plot "g_cas.dat" u 1:3 t "1D FFT", ena(x) t "$\\mathcal{O}(n^3)$", \
"g_cas.dat" u 1:5 t "2D FFT", dva(x) t "$\\mathcal{O}(n^2)$", \
"g_cas.dat" u 1:7 t "SOR", tri(x) t "$\\mathcal{O}(n^4)$"

set output "g_pretok.tex"
unset logscale
set logscale x
set key top right
set ylabel "$\\Phi$"

plot "g_cas.dat" u 1:2 t "1D FFT", \
"g_cas.dat" u 1:4 t "2D FFT", \
"g_cas.dat" u 1:($6**2*1.0/$6) t "SOR"

set output "g_pretok_log.tex"
set logscale xy
set ylabel "$|\\Phi - \\Phi_\\Box|$"
Fi0 = 0.0351342
n(x) = abs(x**2/x-Fi0)

p1(x) = D1*x**c1
p2(x) = D2*x**c2
p3(x) = D3*x**c3

fit p1(x) "g_cas.dat" u 1:(n($2)) via D1,c1
fit p2(x) "g_cas.dat" u 1:(n($4)) via D2,c2
fit p3(x) "g_cas.dat" u 1:(n($6)) via D3,c3

plot "g_cas.dat" u 1:(n($2)) t "1D FFT", p1(x) t sprintf("$\\beta = %.2g$", c1), \
"g_cas.dat" u 1:(n($4)) t "2D FFT", p2(x) t sprintf("$\\beta = %.2g$", c2), \
"g_cas.dat" u 1:(n($6)) t "SOR"

unset logscale
set terminal png crop size 800,800
set contour
set pm3d map

unset xlabel
unset ylabel
unset zlabel
unset xtics
unset ytics
unset ztics

set size square

set output "g_enakomerna.png"
splot "g_enakomerna.dat" matrix w pm3d notitle

set output "g_obtezena_2.png"
splot "g_opna_obtezena_2_1024_2d.dat" matrix w pm3d notitle

set output "g_obtezena_05.png"
splot "g_opna_obtezena_0.5_1024_2d.dat" matrix w pm3d notitle

set output "g_valj.png"
splot "g_valj_128.dat" matrix w pm3d notitle

unset colorbox
set output "g_opna.png"
splot "g_opna.dat" matrix w pm3d notitle nocontour

