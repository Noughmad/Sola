set terminal epslatex color solid

set output "g_hitrost.tex"
set logscale xy
set xlabel "$n$"
set ylabel "Cas [s]"


dva(x) = A + B*x**2
ena(x) = C + D*x**2*log(x)
tri(x) = E + F*x**3

fit ena(x) "g_cas.dat" u 1:3:(sqrt($3)) via C,D
fit dva(x) "g_cas.dat" u 1:5:(sqrt($5)) via A,B
fit tri(x) "g_cas.dat" u 1:7:(sqrt($7)) via E,F

plot "g_cas.dat" u 1:3 t "1D FFT", ena(x) t "$\\mathcal{O}(n^2)$", \
"g_cas.dat" u 1:5 t "2D FFT", dva(x) t "$\\mathcal{O}(n^2)$", \
"g_cas.dat" u 1:7 t "SOR", tri(x) t "$\\mathcal{O}(n^3)$"

set output "g_pretok.tex"
unset logscale
set logscale x

p1(x) = P1 + D1*x**c1
p2(x) = P2 + D2*x**c2
p3(x) = P3 + D3*x**c3

fit p1(x) "g_cas.dat" u 1:2 via P1,D1,c1
fit p2(x) "g_cas.dat" u 1:4 via P2,D2,c2
fit p3(x) "g_cas.dat" u 1:6 via P3,D3,c3

plot "g_cas.dat" u 1:2 t "1D FFT", p1(x) t sprintf("$\\beta = %g$", c1), \
"g_cas.dat" u 1:4 t "2D FFT", p2(x) t sprintf("$\\beta = %g$", c2), \
"g_cas.dat" u 1:6 t "SOR", p3(x) t sprintf("$\\beta = %g$", c3)

set output "g_pretok_log.tex"
set logscale xy
Fi0 = 0.0351342
n(x) = abs(x-Fi0)

p1(x) = D1*x**c1
p2(x) = D2*x**c2
p3(x) = D3*x**c3

fit p1(x) "g_cas.dat" u 1:(n($2)) via D1,c1
fit p2(x) "g_cas.dat" u 1:(n($4)) via D2,c2
fit p3(x) "g_cas.dat" u 1:(n($6)) via D3,c3

plot "g_cas.dat" u 1:(n($2)) t "1D FFT", p1(x) t sprintf("$\\beta = %g$", c1), \
"g_cas.dat" u 1:(n($4)) t "2D FFT", p2(x) t sprintf("$\\beta = %g$", c2), \
"g_cas.dat" u 1:(n($6)) t "SOR", p3(x) t sprintf("$\\beta = %g$", c3)
