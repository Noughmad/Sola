set terminal epslatex color solid
set style data lines

n(x) = x/abs(x) * log(abs(x))

set xlabel "$e$"
set ylabel "$\\left.\\frac{R}{|R|} \\cdot \\ln |R|\\right|_{x=30}$"
set output "g_spekter.tex"

set xzeroaxis
plot[-1.9:] "g_vodik_spekter.dat" u 1:(n($2)) t "Vodik", "g_helij_spekter.dat" u 1:(n($2)) t "Helij"

set output "g_vodik_123.tex"
unset zeroaxis
set xlabel "$x$"
set ylabel "$R(x)$"

plot[0:40] "g_bisekcija_vodik_1_0.dat" u 1:2 t "$n=1, l=0$", \
for [i=0:1] sprintf("g_bisekcija_vodik_2_%d.dat", i) t sprintf("$n=2, l=%d$", i), \
for [i=0:2] sprintf("g_bisekcija_vodik_3_%d.dat", i) t sprintf("$n=3, l=%d$", i)

set output "g_vodik_4.tex"
plot[0:80] for [i=0:3] sprintf("g_bisekcija_vodik_4_%d.dat", i) t sprintf("$n=4, l=%d$", i)

set output "g_vodik_5.tex"
plot[0:100] for [i=0:4] sprintf("g_bisekcija_vodik_5_%d.dat", i) t sprintf("$n=5, l=%d$", i)

set output "g_helij.tex"
plot "g_helij_1.dat" t "$\\varepsilon = -1.8358$", \
"g_helij_2.dat" t "$\\varepsilon = -0.4602$", \
"g_helij_3.dat" t "$\\varepsilon = -0.2039$", \
"g_helij_4.dat" t "$\\varepsilon = -0.1148$"

set output "g_helij_pot.tex"
plot "g_helij_1.dat" u 1:3 t "$\\varepsilon = -1.8358$", \
"g_helij_2.dat" u 1:3 t "$\\varepsilon = -0.4602$", \
"g_helij_3.dat" u 1:3 t "$\\varepsilon = -0.2039$", \
"g_helij_4.dat" u 1:3 t "$\\varepsilon = -0.1148$"