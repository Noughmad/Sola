set terminal epslatex color solid

set key top left

set style data lines
set xlabel "\"Stevilo potez"
set ylabel "$\\langle H \\rangle$"
set output "g_eps.tex"
plot for [eps in "0.1 0.5 1 1.5"] sprintf("g_conv_%s.dat", eps) u 1:2 t sprintf("$\\varepsilon = %s$", eps)

set logscale xy


set xlabel "$\\beta$"
set ylabel "$c_V$"

set output "g_plot_ising_cv.tex"
plot for [N in "32 64 128 256"] sprintf("g_ising_%s.dat",N) u 1:($1**2 * $3 * N) t sprintf("$N = %s$", N)

set ylabel "$\\chi$"
set output "g_plot_ising_chi.tex"
plot for [N in "32 64 128 256"] sprintf("g_ising_%s.dat",N) u 1:($1 * $5 * N) t sprintf("$N = %s$", N)


set logscale xy
set key top right

set ylabel "$\\langle H \\rangle$"

an(x) = 0.5 / tanh(x/2)
set output "g_plot_energy.tex"
plot[0.001:] for [l in "0 0.3 1 3"] sprintf("< sort -g g_energija_%s.dat",l) u 1:4 t sprintf("$\\lambda = %s$", l), \
an(x) ls 7 t "$\\frac{1}{2}\\coth\\frac{\\beta}{2}$"

set ylabel "$\\langle V \\rangle$"

set output "g_plot_pot.tex"
plot[0.001:] for [l in "0 0.3 1 3"] sprintf("< sort -g g_energija_%s.dat",l) u 1:6 t sprintf("$\\lambda = %s$", l)
