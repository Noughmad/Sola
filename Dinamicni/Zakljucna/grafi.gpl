set terminal epslatex color solid

## Delez kaosa
set xlabel "Motnja $\\lambda$"
set ylabel "Dele\"z kaoti\"cnega FP"
set output "g_kaos_1.tex"
plot for [e in "1 3 10 30"] sprintf("Data/g_data_%s_1.dat", e) w lines title sprintf("$E = %s$", e)

set output "g_kaos_3.tex"
plot for [e in "1 3 10 30"] sprintf("Data/g_data_%s_3.dat", e) w lines title sprintf("$E = %s$", e)

set output "g_kaos_5.tex"
plot for [e in "1 3 10 30"] sprintf("Data/g_data_%s_5.dat", e) w lines title sprintf("$E = %s$", e)

## Umeritev alpha
set xlabel "$\\alpha$"
set ylabel "$dP/d\\alpha$"
set output "g_umeritev.tex"
plot for [e in "1 3 10 30"] sprintf("Data/g_histogram_%s_0.dat", e) u ($1/2+$2/2):($3/200) w lines title sprintf("$E = %s$", e)


## Gotovost
set xlabel "$\\lambda$"
set ylabel "Dele\"z kaoti\"cnega FP"

set output "g_gotovost_1.tex"
set key at 1.5, 0.7
plot for [s=1:5] sprintf("Data/g_data_1_%i.dat", s) w lines title sprintf("$\\alpha_c = %i$", s)

set output "g_gotovost_30.tex"
set key at 0.7, 0.28
plot for [s=1:5] sprintf("Data/g_data_30_%i.dat", s) w lines title sprintf("$\\alpha_c = %i$", s)

## Odvisnost od dolzine orbite

unset key
set output "g_dolzina_kaos.tex"
set xlabel "$N$"
set ylabel "Dele\"z kaoti\"cnega FP"

set key at 400,0.5
f(x) = A-B/x
g(x) = BB
fit f(x) "Data/Motnja/g_part.dat" u 1:($2/200) via A,B
fit g(x) "Data/Brez/g_part.dat" u 1:($2/200) via BB
plot "Data/Brez/g_part.dat" u 1:($2/200) with points title "$\\lambda = 0$", g(x) title "konst", \
"Data/Motnja/g_part.dat" u 1:($2/200) with points title "$\\lambda = 0,\\!5$", f(x) title "$a - b/N$"


set ylabel "$dP/d\\alpha$"
set xlabel "$\\alpha$"
set key top right

set output "g_dolzina_motnja.tex"
set key at 4,0.25
plot[][0:0.3] for [i in "50 100 200 350 500"] sprintf("Data/Motnja/g_konv_%s.dat", i) u ($1/2+$2/2):($3/200) w lines title sprintf("$N = %s$", i)

set output "g_dolzina_brez.tex"
set key at 5,0.35
plot for [i in "50 100 200 350 500"] sprintf("Data/Brez/g_konv_%s.dat", i) u ($1/2+$2/2):($3/200) w lines title sprintf("$N = %s$", i)
